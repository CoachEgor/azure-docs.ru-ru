---
title: Возможность в обозреватель данных Azure в машинном обучении
description: Использование машинного обучения, кластеризации для анализа первопричин в обозреватель данных Azure.
author: orspod
ms.author: orspodek
ms.reviewer: jasonh
ms.service: data-explorer
ms.topic: conceptual
ms.date: 04/29/2019
ms.openlocfilehash: bc72cc21ab525ec82d9ce4b24e80ce82d92a5d21
ms.sourcegitcommit: 2ce4f275bc45ef1fb061932634ac0cf04183f181
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/07/2019
ms.locfileid: "65233496"
---
# <a name="machine-learning-capability-in-azure-data-explorer"></a>Возможность в обозреватель данных Azure в машинном обучении

Обозреватель данных Azure, это платформа аналитики больших данных, позволяет отслеживать работоспособность службы QoS и неисправных устройств для Аномальное поведение, используя встроенные [обнаружение аномалий и прогнозирование](/azure/data-explorer/anomaly-detection) функции. После обнаружения аномальный шаблон анализ первопричин (RCA) выполняется, чтобы уменьшить или устранить аномалии.

В процессе диагностики является сложной и очень длинным и done специалистами в области. Этот процесс включает получение и объединение дополнительные данные из различных источников для тот же интервал времени, для поиска изменений в распределение значений для нескольких измерений, построение диаграмм дополнительные переменные и другие функции на основании знания о предметной области и интуиция. Так как эти сценарии диагностики доступны в обозревателе данных Azure, для упрощения фазу диагностики и сократить длительность RCA доступны подключаемые модули обучения машины.

Обозреватель данных Azure имеет три подключаемых модулей машинного обучения: [ `autocluster` ](/azure/kusto/query/autoclusterplugin), [ `basket` ](/azure/kusto/query/basketplugin), и [ `diffpatterns` ](/azure/kusto/query/diffpatternsplugin). Все подключаемые модули реализуют алгоритмы кластеризации. `autocluster` И `basket` подключаемые модули кластера одного набора записей и `diffpatterns` подключаемый модуль кластеров различия между двумя наборами записей.

## <a name="clustering-a-single-record-set"></a>Кластеризации набора одной записи

Распространенный сценарий включает в себя набор данных, выбранных с определенным критериям, например временное окно, в котором видно, Аномальное поведение, показания устройства высокой температуры, длительные команды и верхней расходы пользователей. Мы предлагаем простой и быстрый способ найти общие шаблоны (сегментов) в данных. Шаблоны представляют собой подмножество набора данных, записи которых имеют одинаковые значения через несколько измерений (категориальных столбцов). Следующий запрос строит и показано временных рядов службы исключений через неделю в ячейках десяти минут:

```kusto
let min_t = toscalar(demo_clustering1 | summarize min(PreciseTimeStamp));  
let max_t = toscalar(demo_clustering1 | summarize max(PreciseTimeStamp));  
demo_clustering1
| make-series num=count() on PreciseTimeStamp from min_t to max_t step 10m
| render timechart with(title="Service exceptions over a week, 10 minutes resolution")
```

![Временная диаграмма исключения службы](media/machine-learning-clustering/service-exceptions-timechart.png)

Число исключений службы коррелирует с общей службы трафика. Ясно видно ежедневного шаблон для рабочих дней с момента с понедельника по пятницу, с увеличением числа в службе исключения подсчитывает полдень и удаляет счетчиков ночью. Фиксированная низкой счетчики видимы в выходные. Пики исключения могут быть обнаружены с помощью [время обнаружения аномалий серии](/azure/data-explorer/anomaly-detection?#time-series-anomaly-detection) в обозреватель данных Azure.

Второй всплеск данных происходит на вторник вечера. Следующий запрос используется для дальнейшей диагностики этой копилки. Используйте запрос для перерисовки диаграммы вокруг пик более высокого разрешения (восемь часов в ячейки до минуты) для проверки, является ли резкие пик и просмотреть его границы.

```kusto
let min_t=datetime(2016-08-23 11:00);
demo_clustering1
| make-series num=count() on PreciseTimeStamp from min_t to min_t+8h step 1m
| render timechart with(title="Zoom on the 2nd spike, 1 minute resolution")
```

![Сосредоточиться на временную диаграмму пик](media/machine-learning-clustering/focus-spike-timechart.png)

Мы видим узкий пик двухминутный из 15:00 в 15:02. В следующем запросе число исключений в этом окне двух минут:

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| count
```

|Количество |
|---------|
|972    |

В следующем запросе пример 20 исключений за пределы 972:

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| take 20
```

| PreciseTimeStamp            | Регион | ScaleUnit | DeploymentId                     | Точка трассировки | ServiceHost                          |
|-----------------------------|--------|-----------|----------------------------------|------------|--------------------------------------|
| 2016-08-23 15:00:08.7302460 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 100005     | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:09.9496584 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 8d257da1-7a1c-44f5-9acd-f9e02ff507fd |
| 2016-08-23 15:00:10.5911748 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 100005     | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:12.2957912 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007007   | f855fcef-ebfe-405d-aaf8-9c5e2e43d862 |
| 2016-08-23 15:00:18.5955357 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 9d390e07-417d-42eb-bebd-793965189a28 |
| 2016-08-23 15:00:20.7444854 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 6e54c1c8-42d3-4e4e-8b79-9bb076ca71f1 |
| 2016-08-23 15:00:23.8694999 | eus2   | su2       | 89e2f62a73bb4efd8f545aeae40d7e51 | 36109      | 19422243-19b9-4d85-9ca6-bc961861d287 |
| 2016-08-23 15:00:26.4271786 | ncus   | su1       | e24ef436e02b4823ac5d5b1465a9401e | 36109      | 3271bae4-1c5b-4f73-98ef-cc117e9be914 |
| 2016-08-23 15:00:27.8958124 | scus   | su3       | 90d3d2fc7ecc430c9621ece335651a01 | 904498     | 8cf38575-fca9-48ca-bd7c-21196f6d6765 |
| 2016-08-23 15:00:32.9884969 | scus   | su3       | 90d3d2fc7ecc430c9621ece335651a01 | 10007007   | d5c7c825-9d46-4ab7-a0c1-8e2ac1d83ddb |
| 2016-08-23 15:00:34.5061623 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 1002110    | 55a71811-5ec4-497a-a058-140fb0d611ad |
| 2016-08-23 15:00:37.4490273 | scus   | su3       | 90d3d2fc7ecc430c9621ece335651a01 | 10007006   | f2ee8254-173c-477d-a1de-4902150ea50d |
| 2016-08-23 15:00:41.2431223 | scus   | su3       | 90d3d2fc7ecc430c9621ece335651a01 | 103200     | 8cf38575-fca9-48ca-bd7c-21196f6d6765 |
| 2016-08-23 15:00:47.2983975 | ncus   | su1       | e24ef436e02b4823ac5d5b1465a9401e | 423690590  | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:50.5932834 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 2a41b552-aa19-4987-8cdd-410a3af016ac |
| 2016-08-23 15:00:50.8259021 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 1002110    | 0d56b8e3-470d-4213-91da-97405f8d005e |
| 2016-08-23 15:00:53.2490731 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 36109      | 55a71811-5ec4-497a-a058-140fb0d611ad |
| 2016-08-23 15:00:57.0000946 | eus2   | su2       | 89e2f62a73bb4efd8f545aeae40d7e51 | 64038      | cb55739e-4afe-46a3-970f-1b49d8ee7564 |
| 2016-08-23 15:00:58.2222707 | scus   | Просмотр su5       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007007   | 8215dcf6-2de0-42bd-9c90-181c70486c9c |
| 2016-08-23 15:00:59.9382620 | scus   | su3       | 90d3d2fc7ecc430c9621ece335651a01 | 10007006   | 451e3c4c-0808-4566-a64d-84d85cf30978 |

### <a name="use-autocluster-for-single-record-set-clustering"></a>Использовать autocluster() для одной записи, задайте кластеризации

Несмотря на то, что существуют менее чем тысяче исключения, по-прежнему трудно найти общих сегментов, так как существует несколько значений в каждом столбце. Можно использовать [ `autocluster()` ](/azure/kusto/query/autoclusterplugin) подключаемого модуля, чтобы сразу же извлечь небольшой список общих сегментов и найти интересные кластеров в течение двух минут ее, как показано в следующем запросе:

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| evaluate autocluster()
```

| Идентификатор сегмента | Количество | Процент | Регион | ScaleUnit | DeploymentId | ServiceHost |
|-----------|-------|------------------|--------|-----------|----------------------------------|--------------------------------------|
| 0 | 639 | 65.7407407407407 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 1 | 94 | 9.67078189300411 | scus | Просмотр su5 | 9dbd1b161d5b4779a73cf19a7836ebd6 |  |
| 2 | 82 | 8.43621399176955 | ncus | su1 | e24ef436e02b4823ac5d5b1465a9401e |  |
| 3 | 68 | 6.99588477366255 | scus | su3 | 90d3d2fc7ecc430c9621ece335651a01 |  |
| 4. | 55 | 5.65843621399177 | weu | улучшения su4 | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |

Вы увидите в результатах выше, что чаще всего пользуется сегмент содержит 65.74% записей общее исключение и использует четыре измерения. Следующего сегмента значительно реже, содержит только 9.67% записей и использует три измерения. Другие сегменты еще менее распространены. 

Autocluster использует собственный алгоритм для интеллектуального анализа данных несколько измерений и извлечения интересных сегментов. «Интересные» означает, что каждый сегмент имеет значительные покрытия набора записей и набор функций. Сегменты также различаются, то есть каждый из них является значительно отличается от других. Один или несколько из этих сегментов можно применять для процесса RCA. Чтобы свести к минимуму, оценка и проверка сегмента, autocluster извлекает только небольшой сегмент список.

### <a name="use-basket-for-single-record-set-clustering"></a>Использовать basket() для одной записи, задайте кластеризации

Можно также использовать [ `basket()` ](/azure/kusto/query/basketplugin) подключаемого модуля, как показано в следующем запросе:

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| evaluate basket()
```

| Идентификатор сегмента | Количество | Процент | Регион | ScaleUnit | DeploymentId | Точка трассировки | ServiceHost |
|-----------|-------|------------------|--------|-----------|----------------------------------|------------|--------------------------------------|
| 0 | 639 | 65.7407407407407 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 |  | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 1 | 642 | 66.0493827160494 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 |  |  |
| 2 | 324 | 33.3333333333333 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 | 0 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 3 | 315 | 32.4074074074074 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 | 16108 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 4. | 328 | 33.7448559670782 |  |  |  | 0 |  |
| 5 | 94 | 9.67078189300411 | scus | Просмотр su5 | 9dbd1b161d5b4779a73cf19a7836ebd6 |  |  |
| 6 | 82 | 8.43621399176955 | ncus | su1 | e24ef436e02b4823ac5d5b1465a9401e |  |  |
| 7 | 68 | 6.99588477366255 | scus | su3 | 90d3d2fc7ecc430c9621ece335651a01 |  |  |
| 8 | 167 | 17.1810699588477 | scus |  |  |  |  |
| 9 | 55 | 5.65843621399177 | weu | улучшения su4 | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |  |
| 10 | 92 | 9.46502057613169 |  |  |  | 10007007 |  |
| 11 | 90 | 9.25925925925926 |  |  |  | 10007006 |  |
| 12 | 57 | 5.8641975308642 |  |  |  |  | 00000000-0000-0000-0000-000000000000 |

Basket реализует алгоритм Apriori элемента набора интеллектуального анализа данных и извлекает все сегменты, покрытие набора записей которого превышает пороговое значение (по умолчанию 5%). Вы увидите, что дополнительные сегменты были извлечены с похожих (для примера, сегментов, 0,1 или 2,3).

Подключаемые модули, мощные и простые в использовании, но их существенное ограничение связано с тем, что они кластера одной записи, задавать незащищенных так (с без метки). Неясно, поэтому ли извлеченные шаблоны характеризовать выбранного набора записей (аномальные записей) или глобального набора записей.

## <a name="clustering-the-difference-between-two-records-sets"></a>Кластеризация разницу между наборами двух записей

[ `diffpatterns()` ](/azure/kusto/query/diffpatternsplugin) Подключаемый модуль преодолевает ограничения `autocluster` и `basket`. `Diffpatterns` два набора записей и извлекает основных сегментов, которые различаются между ними. Один набор обычно содержит набор исследуемом аномальных записей (один анализируемой `autocluster` и `basket`). Другой набор записей эталонного набора (базовый). 

В следующем запросе мы используем `diffpatterns` для поиска интересных кластеров в течение двух минут копилки, которые отличаются от кластеров в пределах базового плана. Мы определяем окне базовых показателей как восемь периода неактивности до 15:00 (время запуска пик). Нам также нужно расширить путем двоичного столбца (AB), указав, принадлежит ли определенной записи в базовый план или аномальных набора. `Diffpatterns` реализует алгоритм контролируемого обучения, где создаются метки два класса с подозрительной активности, и флаг базовых показателей (AB).

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
let min_baseline_t=datetime(2016-08-23 14:50);
let max_baseline_t=datetime(2016-08-23 14:58); // Leave a gap between the baseline and the spike to avoid the transition zone.
let splitime=(max_baseline_t+min_peak_t)/2.0;
demo_clustering1
| where (PreciseTimeStamp between(min_baseline_t..max_baseline_t)) or
        (PreciseTimeStamp between(min_peak_t..max_peak_t))
| extend AB=iff(PreciseTimeStamp > splitime, 'Anomaly', 'Baseline')
| evaluate diffpatterns(AB, 'Anomaly', 'Baseline')
```

| Идентификатор сегмента | Число A | Число B | Процент A | Процент B | PercentDiffAB | Регион | ScaleUnit | DeploymentId | Точка трассировки |
|-----------|--------|--------|----------|----------|---------------|--------|-----------|----------------------------------|------------|
| 0 | 639 | 21 | 65.74 | 1.7 | 64.04 | eau | su7 | b5d1d4df547d4a04ac15885617edba57 |  |
| 1 | 167 | 544 | 17.18 | 44.16 | 26.97 | scus |  |  |  |
| 2 | 92 | 356 | 9.47 | 28.9 | 19.43 |  |  |  | 10007007 |
| 3 | 90 | 336 | 9,26 | 27.27 | 18.01 |  |  |  | 10007006 |
| 4. | 82 | 318 | 8.44 | 25.81 | 17.38 | ncus | su1 | e24ef436e02b4823ac5d5b1465a9401e |  |
| 5 | 55 | 252 | 5.66 | 20.45 | 14.8 | weu | улучшения su4 | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |
| 6 | 57 | 204 | 5.86 | 16.56 | 10.69 |  |  |  |  |

Чаще всего пользуется сегмент является сегмента, который был извлечен с `autocluster`, его покрытия в окне аномальных двухминутный также составляет 65.74%. Но его покрытия в окне базовых показателей 8 минутный только % 1.7. Разница в 64.04%. Это различие похоже, относится к аномальных пик. Можно проверить это предположение, разделив исходную диаграмму на записи, относящиеся к этим проблемных сегментом, и другие сегменты, как показано в следующем запросе:

```kusto
let min_t = toscalar(demo_clustering1 | summarize min(PreciseTimeStamp));  
let max_t = toscalar(demo_clustering1 | summarize max(PreciseTimeStamp));  
demo_clustering1
| extend seg = iff(Region == "eau" and ScaleUnit == "su7" and DeploymentId == "b5d1d4df547d4a04ac15885617edba57"
and ServiceHost == "e7f60c5d-4944-42b3-922a-92e98a8e7dec", "Problem", "Normal")
| make-series num=count() on PreciseTimeStamp from min_t to max_t step 10m by seg
| render timechart
```

![Проверка временную диаграмму сегмент «diffpattern»](media/machine-learning-clustering/validating-diffpattern-timechart.png)

На этой диаграмме можно увидеть, что пик на вторник вечера был из-за исключения из этого конкретного сегмента, обнаруженных с помощью `diffpatterns` подключаемого модуля.

## <a name="summary"></a>Сводка

Подключаемые модули машинного обучения обозреватель данных Azure могут быть полезны во многих сценариях. `autocluster` И `basket` реализации алгоритма неконтролируемого обучения на них прост в использовании. `Diffpatterns` реализует алгоритм обучения в защищенном режиме и, хотя более сложные операции, он обладает более широкими возможностями при извлечении дифференциации сегменты для RCA.

Эти подключаемые модули используются интерактивном режиме в сценариях ad-hoc и автоматическое практически в реальном времени службы мониторинга. В обозревателе данных Azure обнаружение аномалий серии время идет процесс диагностики, который оптимизирован для обеспечения соответствия стандартам необходимую производительность.
